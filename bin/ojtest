#!/usr/bin/env objj

@import <Foundation/Foundation.j>

@import <OJUnit/OJTestCase.j>
@import <OJUnit/OJTestSuite.j>
@import <OJUnit/OJTestResult.j>
@import <OJUnit/OJTestListenerText.j>

CPLogRegister(CPLogPrint, "warn");

@implementation OJTestRunnerText : CPObject
{
    OJTestListener _listener;
}

- (id)init
{
    if (self = [super init])
    {
        _listener = [[OJTestListenerText alloc] init];
    }
    return self;
}

- (OJTest)getTest:(CPString)suiteClassName
{
    var testClass = objj_lookUpClass(suiteClassName);
    
    if (testClass)
    {
        var suite = [[OJTestSuite alloc] initWithClass:testClass];
        return suite;
    }
    
    CPLog.warn("unable to get tests");
    return nil;
}

- (void)startWithArguments:(CPArray)args
{
    if (args.length === 0)
    {
        [self report];
        return;
    }

    var testCaseFile = require("file").absolute(args.shift());

    var matches = testCaseFile.match(/([^\/]+)\.j$/);

    system.stderr.write(matches[1]).flush();
    var testCaseClass = matches[1];

    require(testCaseFile);

    var suite = [self getTest:testCaseClass];

    [self run:suite];
    system.stderr.write("\n").flush();

    // run the next test when this is done
    [self startWithArguments:args];
}

- (OJTestResult)run:(OJTest)suite wait:(BOOL)wait
{
    var result = [[OJTestResult alloc] init];
    
    [result addListener:_listener];
    
    [suite run:result];
    
    return result;
}
- (OJTestResult)run:(OJTest)suite
{
    return [self run:suite wait:NO];
}

+ (void)runTest:(OJTest)suite
{
    var runner = [[OJTestRunnerText alloc] init];
    [runner run:suite];
}

+ (void)runClass:(Class)aClass
{
    [self runTest:[[OJTestSuite alloc] initWithClass:aClass]];
}

- (void)report
{
    var totalErrors = [[_listener errors] count] + [[_listener failures] count];

    if (!totalErrors)
        return CPLog.info("End of all tests.");

    CPLog.fatal("Test suite failed with "+[[_listener errors] count]+" errors and "+[[_listener failures] count]+" failures.");
    
    require("os").exit(1);
}

@end

function main(args) {
    runner = [[OJTestRunnerText alloc] init];
    [runner startWithArguments:args.slice(1)];
}
